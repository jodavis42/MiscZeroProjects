//**********************************************************************************
//
// Changes the radius and number of particles on a particle emitter according
// to the peak audio volume.
//
//**********************************************************************************
class MusicVolumeParticles : ZilchComponent
{
  // Requires a SphericalParticleEmitter component
  [Dependency] var ParticleEmitter : SphericalParticleEmitter;
  
  // Value that will be multiplied with peak volume for the emitter size
  [Property] var SizeMultiplier : Real = 50.0;
  // Value that will be multiplied with peak volume for the emit rate
  [Property] var EmitRateMultiplier : Real = 5000.0;
  
  function Initialize(init : CogInitializer)
  {
    // Connect to the LogicUpdate event
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    // Get the current peak volume
    var peakVolume = Audio.PeakOutputLevel;
    // Save the size value
    var size = this.SizeMultiplier * peakVolume;
    // Set the particle emitter size
    this.ParticleEmitter.EmitterSize = Real3(size, size, 0.0);
    // Set the particle emitter emit rate
    this.ParticleEmitter.EmitRate = this.EmitRateMultiplier * peakVolume;
  }
}

class Cannon : ZilchComponent
{
    [Property]
    var Speed : Real = 100.0;
    [Property]
    var Projectile : Archetype = Archetype.Find("Bullet");
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, "Activate", this.OnActivate);
    }

    function OnActivate(e : PlayerEvent)
    {
        var worldPosition = this.Owner.Transform.WorldTranslation;
        var bullet = this.Space.Create(this.Projectile);
        
        bullet.Transform.WorldTranslation = worldPosition;
        bullet.Transform.WorldRotation = this.Owner.Transform.WorldRotation;
        bullet.RigidBody.Velocity = this.Owner.Orientation.WorldForward * local Real3(this.Speed);
        Team.SetTeam(bullet, this.Owner);
        
        this.Owner.SimpleSound.Play();
        
        var body = this.Owner.Collider.ActiveBody;
        if (body != null)
        {
            var cannonVelocity = body.GetPointVelocity(this.Owner.Transform.WorldTranslation);
            bullet.RigidBody.Velocity += cannonVelocity;
        }
    }
}

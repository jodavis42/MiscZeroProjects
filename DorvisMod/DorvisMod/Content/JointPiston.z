class JointPiston : ZilchComponent
{
    var Root : Cog = null;
    var Joint : Cog = null;
    var Motorized : Boolean = true;

    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, Events.Attached, this.OnAttached);
        Zero.Connect(this.Owner, "Activate", this.OnActivate);
        Zero.Connect(this.Owner, "Deactivate", this.OnDeactivate);
        
        //Debugging
        //Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    //function OnLogicUpdate(e : UpdateEvent)
    //{
    //    if (this.Joint != null)
    //    {
    //        this.Joint.DebugDraw();
    //    }
    //}
    
    function OnAttached(e : HierarchyEvent)
    {
        this.CreateJoint();
    }
    
    function OnActivate(e : Event)
    {
        var motor = this.Joint.JointMotor;
        
        if (motor.Speed < 0.0)
        {
            motor.Speed = 5.0;
        }
        else
        {
            motor.Speed = -5.0;
        }
    }

    function OnDeactivate(e : Event)
    {
        
    }
    
    function CreateJoint()
    {
        this.Owner.RigidBody.Static = false;
        var parent = this.Owner.Parent;
        
        var axis = this.Owner.Orientation.WorldUp;
        
        var jointCreator = Zero.JointBuilder.CreateInstance();
        var worldPoint = this.Owner.Transform.WorldTranslation;
        this.Joint = jointCreator.AttachWorld(parent, this.Owner, worldPoint, "PrismaticJoint");
        if(this.Joint == null)
        {
            return;
        }
        
        var revoluteJoint = this.Joint.PrismaticJoint;
        
        revoluteJoint.WorldAxis = axis;
        revoluteJoint.LocalPointA *= 1.02;
        revoluteJoint.LocalPointB *= 1.02;
        
        if (this.Motorized == true)
        {
            jointCreator.AttachMotor(this.Joint);
            var jointMotor = this.Joint.JointMotor;
            jointMotor.MaxForce = 50.0;
            jointMotor.Speed = 0.0;
            
            jointCreator.AttachLimit(this.Joint);
            var jointLimit = this.Joint.JointLimit;
            jointLimit.LowerLimit = 0.0;
            jointLimit.UpperLimit = 3.0;
        }
    }
}
